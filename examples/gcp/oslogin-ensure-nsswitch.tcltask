#!/usr/bin/env TclTaskRunner.tcl
# -*- coding: utf-8; mode: tcl -*-

import {
    scope_guard
} from utils.tcl

variable nsswitchFn /etc/nsswitch.conf
variable osloginDBNames {passwd group}
variable osloginSources {cache_oslogin oslogin}

default target MAIN check {
    set missing [$self list-missing-oslogin-sources]
    list [expr {$missing eq ""}] missing $missing
} action {
    # XXX: 理想的には missing のみを補うべきだが、そこまで手間を掛ける余裕はないので手抜き
    $self update-nsswitch-for-oslogin
}

#
# passwd,group の行に cache_oslogin oslogin を追記する
#
method update-nsswitch-for-oslogin  {{fn ""}} {
    if {$fn eq ""} {
        set fn $nsswitchFn
    }

    set inplace [if {![IS_DRY_RUN]} {
        list -i
    }]

    set map [list @SRCS@ $osloginSources \
                 @PATTERN@ [join $osloginDBNames |]]

    set script [list perl {*}$inplace -ple [string map $map {
        if (/^(@PATTERN@):/) { $_ .= " @SRCS@"}
    }] $fn]

    puts "# $script"
    exec {*}$script >@ stdout 2>@ stderr
}

method list-missing-oslogin-sources {{fn ""}} {
    if {$fn eq ""} {
        set fn $nsswitchFn
    }
    set dict [$self find-nsswitch-items $fn {*}$osloginDBNames]

    set missing []
    foreach db $osloginDBNames {
        set enabledSources [dict get $dict $db]
        foreach src $osloginSources {
            if {$src in $enabledSources} continue
            dict lappend missing $db $src
        }
    }

    set missing
}

method find-nsswitch-items {fn args} {
    set fh [open $fn]
    scope_guard fh [list close $fh]

    set result []
    while {[gets $fh line] >= 0} {
        if {![regexp {^(\w+):\s*(\S.*)} $line -> key value]} continue
        if {$key in $args} {
            dict set result $key $value
        }
    }
    set result
}
